apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {

    compileSdkVersion 26

    defaultConfig {
        applicationId "com.efemoney.ussdtoolbox"

        minSdkVersion 19
        targetSdkVersion 26

        versionCode 1
        versionName "1.0"

        vectorDrawables.useSupportLibrary = true
    }

    dataBinding.enabled = true

    buildTypes {

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions 'data-source'

    productFlavors {

        local {
            dimension 'data-source'
            resValue 'bool', 'show_debug_dialog', 'true'
        }

        live {
            dimension 'data-source'
            resValue 'bool', 'show_debug_dialog', 'false'
        }
    }

    variantFilter { variant ->

        // Remove localRelease as it's not needed
        // In groovy '==' calls '.equals' internally
        if (variant.name == 'localRelease') variant.setIgnore(true)
    }
}

dependencies {

    kapt "com.android.databinding:compiler:$versions.agp"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$versions.kotlin"

    implementation "com.android.support:design:$versions.supportLibrary"
    implementation "com.android.support:cardview-v7:$versions.supportLibrary"
    implementation "com.android.support:appcompat-v7:$versions.supportLibrary"

    implementation "com.google.firebase:firebase-crash:$versions.playServices"

    liveImplementation "com.google.firebase:firebase-database:$versions.playServices"
    localImplementation "com.google.code.gson:gson:$versions.gson"

    implementation "io.reactivex.rxjava2:rxjava:$versions.rxJava"
    implementation "io.reactivex.rxjava2:rxandroid:$versions.rxAndroid"

    implementation "com.google.dagger:dagger:$versions.dagger"
    kapt "com.google.dagger:dagger-compiler:$versions.dagger"
    compileOnly 'javax.annotation:jsr250-api:1.0'

    // Dependencies for local unit tests
    testImplementation "junit:junit:$versions.jUnit"
}

apply plugin: 'com.google.gms.google-services'

